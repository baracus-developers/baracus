#!/usr/bin/perl -w

use strict;

use Getopt::Long qw( :config pass_through );
use Pod::Usage;
use LWP;
use HTTP::Cookies;
use Term::ReadKey;
use XML::Simple;

use lib "/home/cruejones/git/baracus/Baracus/lib";

use Dancer qw( :syntax);
use Baracus::Core   qw( :subs );
use Baracus::Config qw( :vars :subs );
use Baracus::State  qw( :vars :admin );

=pod

=head1 NAME

B<bahost> - baracus tool to manage physical node entries.

=head1 SYNOPSIS

B<bahost> E<lt>commandE<gt> [subcommand] [options and arguments]

Where E<lt>commandE<gt> is one of

  add     --mac <mac>    add a new physical device via mac entry
  remove  --mac <mac>    delete a physical device via mac entry
  enable  --mac <mac>    enable a physical device via mac entry
  disable --mac <mac>    disable a physical device via mac entry

  list nodes             list registered node entires
  list states            list state data on registered node entries
  list templates         list build templates assigned to node entries

  detail node <...>      display detailed state data for a physical node entry
  detail inventory <...> display hardware inventory for a physical node entry

And, where <...> indicates additional required arguments.

Use 'man' or 'help <command> [<type>]' for more details.

=head1 DESCRIPTION

This tool allows administrators to add/delete and enable/disable physical devices.
Additionally this tool allows for listing of registered nodes, their states and inventories.
Active physical device entries must be assigned an 'action' via bado to boot accordingly.

=head1 OPTIONS

=over 4

=item -v --verbose  Be verbose with output

=item -q --quiet Be as quiet as possible

=item -n --nolabels  Do not print headers/footers

=back

=cut

my $apiver = "api/v1";
my $man  = 0;
my $help = 0;

my $cmds = {
            'add'       => \&add,
            'remove'    => \&remove,
            'list'      => \&list,
            'detail'    => \&detail,
            'inventory' => \&inventory,
            'enable'    => \&enable,
            'disable'   => \&disable
            };

my $opts = {
            verbose    => 0,
            quiet      => 0,
            all        => 0,
            nolabels   => 0,
            debug      => 0,
            execname   => "",
            LASTERROR  => "",
            };

# Build command line for help, historical reference and point-in-time rebuilding
$opts->{execname} = $0;
$opts->{execname} = $1 if ( $0 =~ m|^.*/([^/].+)| );

GetOptions(
           'verbose'   => \$opts->{verbose},
           'quiet'     => \$opts->{quiet},
           'all'       => \$opts->{all},
           'nolabels'  => \$opts->{nolabels},
           'debug+'    => \$opts->{debug},
           'man'       => \$man,
           'help|?'    => \$help,
           );

&man()  if $man;
&help( $opts, $cmds ) if $help;
&help( $opts, $cmds ) unless ( scalar @ARGV );
if ($ARGV[0] eq "man"  ) { &man(); }
if ($ARGV[0] eq "help" ) { shift @ARGV; &help( $opts, $cmds, @ARGV ); }

## Setup base URL and PORT
my $port = undef;
if ( $baVar{port} ) {
    $port = $baVar{port};
} else {
    $port = "80";
}

my $baseurl = undef;
if ( $baVar{url} ) {
    $baseurl = $baVar{url};
} elsif ( $baVar{serverip} ) {
    $baseurl = $baVar{serverip};
} else {
    print "Edit /etc/sysconfig/baracus to include a ServerIP or URL\n";
    exit 1;
}

print "Using: $baseurl \n" if ($opts->{debug});
print "Using: $port\n" if ($opts->{debug});

# Setup LWP
my $browser = LWP::UserAgent->new( );
$browser->cookie_jar( {} );
$browser->agent('baracus');
$browser->default_header('Accept' => 'application/json');

## Authenticate request
my $username = "";
my $password = "";
if ( -f "$ENV{HOME}/.barc") {
    print "We have a file\n" if ($opts->{debug});
    open(FILE, "<$ENV{HOME}/.barc");
    while (<FILE>) {
        if (/username/) { chomp ( $username = (split /=/, $_)[1] ); }
        if (/password/) { chomp ( $password = (split /=/, $_)[1] ); }
    }
} else {
    print "enter username: ";
    chomp($username = ReadLine 0);

    print "enter password: ";
    ReadMode 'noecho';
    chomp($password = ReadLine 0);
    ReadMode 'normal';
    print "\n";
}
my $login_url = "http://$baseurl:$port/login?username=$username&password=$password";
my $login_response = $browser->post( $login_url );

my $status = &main( $opts, $cmds, @ARGV );

my $logout_url = "http://$baseurl:$port/logout";
my $logout_response = $browser->get( $logout_url );
print $logout_response->content;

exit $status;

die "DOES NOT EXECUTE\n";

###########################################################################
##
## commands
##

=head1 COMMANDS

=head2 add --mac <mac> [ --hostname <name> ]

Add node (via mac) entry to Baracus.

Argument

  --mac <addr>             MAC address to be added

Option

  --hostname <name>        Provide the hostname as an alias to refer to this host

=cut


sub add() {

    my $verb = 'add';

    my $mac;
    my $hostname;

    @ARGV = @_;
    GetOptions(
               'mac=s'      => \$mac,
               'hostname=s' => \$hostname,
               );

    unless ( defined $mac ) {
        print "--mac <mac> required\n";
        return 1;
    }

    if ( scalar @ARGV ) {
            printf "\nUnknown arg(s): %s\n", join(" ", @ARGV);
            &help( $opts, $cmds, $verb );
    }

    my $json_hash = {};
    $json_hash->{hostname} = $hostname;
    $json_hash->{mac}      = $mac;

    my $url = "http://$baseurl:$port/$apiver/host";
    my $message = to_json($json_hash);

    my $req = HTTP::Request->new( 'POST', $url );
    $req->content_type('application/json');
    $req->content($message);

    my $response = $browser->request( $req );
    die "Can't get $url -- ", $response->status_line
      unless $response->is_success;

    die "Hey, I was expecting JSON, not ", $response->content_type
      unless $response->content_type eq 'application/json';

    my $data = from_json( $response->content );
    print "( $data->{mac} ) $data->{hostname}  :  $data->{action} $data->{result} \n";

    return 0;

}

=head2 remove { --mac <mac> | --hostname <name> }

Remove host entry from Baracus.  Specify the device by --mac or if a hostname has been associated with this mac you can instead specify --hostname to locate the entry to be removed.

=cut

sub remove()
{

    my $verb = 'remove';

    my $mac;
    my $hostname;

    @ARGV = @_;
    GetOptions(
               'mac=s'      => \$mac,
               'hostname=s' => \$hostname,
               );

    unless ( ( defined $mac ) or ( defined $hostname ) ) {
        print "--mac <mac> or --hostname <hostname> required\n";
        return 1;
    }

    if ( ( defined $mac ) and ( defined $hostname ) ) {
        print "--mac and --hostname cannot be combined\n";
        return 1;
    }

    if ( scalar @ARGV ) {
            printf "\nUnknown arg(s): %s\n", join(" ", @ARGV);
            &help( $opts, $cmds, $verb );
    }

     my $bytype;
    my $node;
    if ( $mac ne "" ) {
        $bytype = "mac";
        $node = $mac;
    } elsif ( $hostname ne "" ) {
        $bytype = "host";
        $node = $hostname;
    }

    my $url = "http://$baseurl:$port/$apiver/host/by-$bytype/$node";

    my $req = HTTP::Request->new( 'DELETE', $url );
    $req->content_type('application/json');

    my $response = $browser->request( $req );
    die "Can't get $url -- ", $response->status_line
      unless $response->is_success;

    die "Hey, I was expecting JSON, not ", $response->content_type
      unless $response->content_type eq 'application/json';

    my $data = from_json( $response->content );
    print "( $data->{mac} ) $data->{hostname}  :  $data->{action} $data->{result} \n";

    return 0;

}

=head2 list { nodes | states | templates } [ --filter <spec> ]

Types

  nodes               shows all nodes
  states              state data releated to hosts
  templates           build client templates

Option

  --filter <spec>     exact, or partial with wildcarded ('*')

Where <spec> is of format:

  mac::<address>
  hostname::<name>

=cut

sub list
{
    my $verb = "list";
    my $listtype = shift;

    my $filter = "*";

    @ARGV = @_;

    GetOptions(
               'filter=s'  => \$filter,
               );
 
    unless ( defined $listtype ) {
        die "listtype required (ie. states/nodes/templates) \n";
    }

    if ( scalar @ARGV ) {
            printf "\nUnknown arg(s): %s\n", join(" ", @ARGV);
            &help( $opts, $cmds, $verb );
    }

    my $url = "http://$baseurl:$port/$apiver/host/$listtype/$filter";

    my $req = HTTP::Request->new( 'GET', $url );
    $req->header( 'Accept' => 'application/json' );

    my $response = $browser->request( $req );
    die "Can't get $url -- ", $response->status_line
      unless $response->is_success;

    die "Hey, I was expecting JSON, not ", $response->content_type
      unless $response->content_type eq 'application/json';

    my $data = from_json( $response->content );

    if ( $listtype eq "templates" ) {
        ## List available build templates filtered by hostname if provided
        ##
        unless ( $opts->{quiet} || $opts->{nolabels} ) {
            print "---------------------------------------------------------------------------\n";
            printf "%-17s %-17s %-9s\n", "mac", "hostname", "autobuild";
            print "---------------------------------------------------------------------------\n";
        }

        for my $node ( sort keys %$data ) {
            printf "%-17s %-17s %-9s \n", "$data->{$node}->{mac}", "$data->{$node}->{name}", "$data->{$node}->{auto}";
        }

    } elsif ( $listtype eq "states" ) {
        ## List macs and show state and when time of that state
        ##
        if ( not ( $opts->{quiet} or $opts->{nolabels} ) )
        {
            my $line = "------------------------------------------------------------";
            if ( $opts->{verbose} ) {
                $line .= "--------------------------";
            }
            if ( ! $opts->{quiet} ) {
                $line .= "-----------------";
            }
            print $line . "\n";
            if ( $opts->{verbose} ) {
                printf "%-28s", "time ";
            }
            printf "%-17s ", "mac";
            if ( ! $opts->{quiet} ) {
                printf "%-17s ", "hostname";
            }
            printf "%-10s %-10s %-9s %-9s\n",
                "pxecurr", "pxenext", "state", "active";
            print $line . "\n";
        }

        for my $node ( sort keys %$data ) {

            printf "%-17s ", $data->{$node}->{mac};
            if ( ! $opts->{quiet} ) {
                printf "%-17s ", $data->{$node}->{hostname};
            }
            printf "%-10s %-10s %-9s %-9s\n",
                "$data->{$node}->{pxecurr_str}", "$data->{$node}->{pxenext_str}", "$data->{$node}->{state_str}", "$data->{$node}->{active_str}";
        }
    } elsif ( $listtype eq "nodes" ) {
 
        if (not ( $opts->{quiet} or $opts->{nolabels} ) ) {
            print "---------------------------------------------------------------\n";
            printf "%-17s %-17s %-10s %-9s\n", "mac", "hostname", "inventory", "active";
            print "---------------------------------------------------------------\n";
        }

        my $inventory = "";
        my $inventory_st = "";
        my $sel;

        for my $node ( sort keys %$data ) {
            printf "%-17s ", $node;
            printf "%-17s ", $data->{$node}->{hostname};
            printf "%-10s ", $data->{$node}->{inventory_st};
            printf "%-9s",   $data->{$node}->{bstate};
            printf "\n";
        }
    }

    return 0;  
}

=head2 detail { node | inventory } { --mac <mac> | --hostname <name> }

Types

  node               full details of client
  inventory          hardware inventory for client

Argument

  --mac <mac>        provide the mac to identify client of interest
  --hostname <name>  provide the hostname to identify client of interest
                     note that not all clients have associated hostname info

=cut

sub detail
{
    my $verb = "detail";
    my $hostname = "";
    my $mac = "";

    @ARGV = @_;
    GetOptions(
               'mac=s'      => \$mac,
               'hostname=s' => \$hostname,
               );

    if ( scalar @ARGV ) {
        printf "\nUnknown arg(s): %s\n", join(" ", @ARGV);
        &help( $opts, $verb );
    }

    my $bytype;
    my $node;
    if ( $mac ne "" ) {
        $bytype = "mac";
        $node = $mac;
    } elsif ( $hostname ne "" ) {
        $bytype = "host";
        $node = $hostname;
    }

    my $url = "http://$baseurl:$port/$apiver/host/$verb/by-$bytype/$node";

    my $req = HTTP::Request->new( 'GET', $url );
    $req->header( 'Accept' => 'application/json' );

    my $response = $browser->request( $req );
    die "Can't get $url -- ", $response->status_line
      unless $response->is_success;

    die "Hey, I was expecting JSON, not ", $response->content_type
      unless $response->content_type eq 'application/json';

    my $data = from_json( $response->content );

    print "Host Entry:\n";
    print "-----------\n";
    printf "%-11s", "MAC:";      print "$data->{mac}" if $data->{mac}; print "\n";
    printf "%-11s", "Hostname:"; print "$data->{hostname}" if $data->{hostname}; print "\n";
    printf "%-11s", "IP Addr:";  print "$data->{ip}" if $data->{ip}; print "\n";
    print "\n";
    print "Status: \n";
    print "------- \n";
    printf "%-10s", "state:";   print" $data->{oper} " if $data->{oper}; print "\n";
    printf "%-10s", "active:";  print" $data->{admin} " if $data->{admin}; print "\n";
    printf "%-10s", "pxecurr:"; print" $data->{pxecurr} " if $data->{pxecurr}; print "\n";
    printf "%-10s", "pxenext:"; print" $data->{pxenext} " if $data->{pxenext}; print "\n";
    print "\n";
    print "Build\n";
    print "-----\n";
    printf "%-18s", "distro:";            print " $data->{distro}" if $data->{distro}; print "\n";
    printf "%-18s", "addons:";            print " $data->{addons}" if $data->{addons}; print "\n";
    printf "%-18s", "hardware:";          print " (ver $data->{hardware_ver}) $data->{hardware}" if $data->{hardware}; print "\n";
    printf "%-18s", "profile:";           print " (ver $data->{profile_ver}) $data->{profile}" if $data->{profile}; print "\n";
    printf "%-18s", "autobuild:";         print " (ver $data->{autobuild_ver}) $data->{autobuild}\n" if $data->{autobuild}; print "\n";
    printf "%-18s", "vars:";              print " $data->{vars}" if $data->{vars}; print "\n";

    print "\n";
    if ( $data->{modules} ) {
## This is an array, need to update to handle as such
        print "Build Modules\n";
        print "-------------\n";
        printf "%-18s", "modules:";
        printf "\n%-18s $data->{modules}", "";
        print "\n";
        print "\n";
    }

    if ( $data->{storageid} ) {
        print "Storage\n";
        print "-------\n";
        printf "%-18s", "StorageID:"; print " $data->{storageid}";
        print "\n";
        print "\n";
    }

    if ( $data->{mcastid} ) {
        print "MCast\n";
        print "-----\n";
        printf "%-18s", "MultiCastID:"; print " $data->{mcastid}";
        print "\n";
        print "\n";
    }

    print "Additional Details\n";
    print "------------------\n";
    printf "%-18s", "Loghost:";           print " $data->{loghost}"  if $data->{loghost}; print "\n";
    printf "%-18s", "Remote Access:";     print " $data->{raccess}"  if $data->{raccess}; print "\n";
    printf "%-18s", "Auto Wipe:";         print " $data->{autonuke}" if $data->{autonuke}; print "\n";
    printf "%-18s", "Entry Creation:";    print " $data->{creation}" if $data->{creation}; print "\n";
    printf "%-18s", "Last Modification:"; print " $data->{change}"   if $data->{change}; print "\n";

    return 0;

}

=head2 enable { --mac <addr> | --hostname <name> }

Enable the specified host node.  This will update the admin state of
the node to enabled.

=cut

=head2 disable { --mac <addr> | --hostname <name> }

Disable the specified host node.  This will update the admin state to
disabled and serve pxewait boot menus to pxebooting disabled nodes.

=cut

sub enable  { &_enable_disable_( "enable",  @_ );}
sub disable { &_enable_disable_( "disable", @_ );}

sub _enable_disable_
{

    my $verb = shift;

    unless ( scalar @_ ) {
        &help( $opts, $cmds, $verb );
    }

    my $hostname;
    my $mac;

    @ARGV = @_;

    GetOptions(
               'mac=s'          => \$mac,
               'hostname=s'     => \$hostname,
               );

    unless ( ( defined $mac ) or ( defined $hostname ) ) {
        print "--mac <mac> or --hostname required\n";
        return 1;
    }

    if ( ( defined $mac ) and ( defined $hostname ) ) {
        print "--mac and --hostname cannot be combined\n";
        return 1;
    }

    if ( scalar @ARGV ) {
            printf "\nUnknown arg(s): %s\n", join(" ", @ARGV);
            &help( $opts, $cmds, $verb );
    }

    my $json_hash = {};
    $json_hash->{verb}     = $verb;
    $json_hash->{hostname} = $hostname if ( defined $hostname );
    $json_hash->{mac}      = $mac      if ( defined $mac );

    my $url = "http://$baseurl:$port/$apiver/host";
    my $message = to_json($json_hash);

    my $req = HTTP::Request->new( 'PUT', $url );
    $req->content_type('application/json');
    $req->content($message);

    my $response = $browser->request( $req );
    die "Can't get $url -- ", $response->status_line
      unless $response->is_success;

    die "Hey, I was expecting JSON, not ", $response->content_type
      unless $response->content_type eq 'application/json';

    my $data = from_json( $response->content );
    print "( $data->{mac} ) $data->{hostname}  :  $data->{action} $data->{result} \n";

    return 0;


}


######################################################################

die "ABSOLUTELY DOES NOT EXECUTE";

__END__
